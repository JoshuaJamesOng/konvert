apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion androidVersions.compileSdk
    buildToolsVersion androidVersions.buildTools

    defaultConfig {
        applicationId "com.ongtonnesoup.konvert"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.ongtonnesoup.konvert.TestJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
            jacocoOptions {
                // here goes all jacoco config, for example
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
                unitTests.all {
                    testLogging.events = ["passed", "skipped", "failed"]
                }
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    flavorDimensions "environment", "version"
    productFlavors {
        production {
            dimension "environment"
            buildConfigField "boolean", "USE_FIXED_EXCHANGE_RATES", "false"
        }
        development {
            dimension "environment"
            buildConfigField "boolean", "USE_FIXED_EXCHANGE_RATES", "true"
        }
        ocr {
            dimension "version"
            buildConfigField "boolean", "USE_OCR", "true"
        }
        base {
            dimension "version"
            buildConfigField "boolean", "USE_OCR", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

androidExtensions {
    experimental = true
}

def arrow_version = "0.9.0" // TODO Move
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':settings')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'com.ww:roxie:0.4.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha03'
    implementation 'androidx.core:core-ktx:1.3.0-beta01'

    implementation 'com.github.ajalt:timberkt:1.5.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.google.android.gms:play-services-vision:19.0.0'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.0-alpha01"

    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    implementation 'androidx.room:room-rxjava2:2.2.5'

    implementation 'androidx.work:work-runtime-ktx:2.3.4'

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0-alpha04"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0-alpha04"

    implementation "androidx.preference:preference-ktx:1.1.0"

    implementation 'com.google.dagger:dagger:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'

    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    implementation "io.arrow-kt:arrow-typeclasses:$arrow_version"

    implementation 'com.google.android.play:core:1.7.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation 'org.amshove.kluent:kluent-android:1.49'
    testImplementation 'com.rubylichtenstein:rxtest:1.0.7'

    testImplementation 'org.jetbrains.spek:spek-api:1.2.1' // TODO deprecate
    testImplementation 'org.jetbrains.spek:spek-junit-platform-engine:1.2.1' // TODO deprecate
    testImplementation 'org.junit.platform:junit-platform-runner:1.0.1'

    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.2'
    testImplementation 'org.spekframework.spek2:spek-runner-junit5:2.0.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"

    androidTestImplementation 'androidx.test:rules:1.3.0-alpha05'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha05'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha05'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha05'
    androidTestImplementation 'androidx.work:work-testing:2.3.4'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'com.google.dagger:dagger:2.22.1'
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.22.1'
    androidTestUtil 'androidx.test:orchestrator:1.3.0-alpha05'
}
